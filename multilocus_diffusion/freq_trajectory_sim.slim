// Simulate WF model with two loci, print out frequency trajectory
// Print out genotype matrix --> grep the OUT when running this script on terminal and save as .txt file
initialize() {
	if (!exists("MU1")) defineConstant("MU1", 1e-5);
	if (!exists("MU2")) defineConstant("MU2", 1e-5);
	if (!exists("S1")) defineConstant("S1", 0.02);
	if (!exists("S2")) defineConstant("S2", 0.01);
	if (!exists("H1")) defineConstant("H1", 0.5); // dominance factor 1
	if (!exists("H2")) defineConstant("H2", 0.5); // dominance factor 2
	if (!exists("R")) defineConstant("R", 0.01);
	if (!exists("N")) defineConstant("N", 100000);
	if (!exists("LOGPATH")) defineConstant("LOGPATH", "./sim_log.txt");
	initializeMutationRate(c(MU1, MU2), c(0, 1));
	initializeMutationType("m1", S1, "f", H1);  // locus 1
	initializeMutationType("m2", S2, "f", H2); // locus 2
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 0);
	initializeGenomicElementType("g2", m2, 1.0);
	initializeGenomicElement(g2, 1, 1);
	initializeRecombinationRate(R);
}
1 early() {
	sim.addSubpop("p1", N);
   catn("fAABB, fAABb, fAAbb, fAaBB, fAaBb, fAabb, faaBB, faaBb, faabb"); 
}

1:10000 late() {
	/// write f1 ~ f9 to log file
	m1_fixed = (sum(sim.substitutions.mutationType == m1) == 1);
	if (m1_fixed) {
		homo_dom_1 = matrix(rep(1, N), ncol = 1);
		hetero_1 = matrix(rep(0, N), ncol = 1);
		homo_rec_1 = matrix(rep(0, N), ncol = 1);
		// Once a derived allele on locus 1 is fixed, no more mutation will happen on the same locus (reasonable assumption as we increase the number of loci)
		mut1s = p1.genomes.mutationsOfType(m1);
		if (size(mut1s) > 0){		
			p1.genomes.removeMutations(mut1s);
			}
	}
	else{
		has_mut1 = t(matrix(asInteger(p1.genomes.containsMarkerMutation(m1, 0)), nrow=2));
		homo_hetero_mut1 = has_mut1[,0] + has_mut1[,1];
		homo_dom_1 = asInteger(homo_hetero_mut1 == 2);
		hetero_1 = asInteger(homo_hetero_mut1 == 1);
		homo_rec_1 = asInteger(homo_hetero_mut1 == 0);
	}
	m2_fixed = (sum(sim.substitutions.mutationType == m2) == 1);
	if (m2_fixed) {
		homo_dom_2 = matrix(rep(1, N), ncol = 1);
		hetero_2 = matrix(rep(0, N), ncol = 1);
		homo_rec_2 = matrix(rep(0, N), ncol = 1);
		mut2s = p1.genomes.mutationsOfType(m2);
		if (size(mut2s) > 0){
			p1.genomes.removeMutations(mut2s);
			}
		}
	else{
		has_mut2 = t(matrix(asInteger(p1.genomes.containsMarkerMutation(m2, 1)), nrow=2));
		homo_hetero_mut2 = has_mut2[,0] + has_mut2[,1];
		homo_dom_2 = asInteger(homo_hetero_mut2 == 2);
		hetero_2 = asInteger(homo_hetero_mut2 == 1);
		homo_rec_2 = asInteger(homo_hetero_mut2 == 0);
	}
	mut1_matrix = matrix(c(homo_dom_1, hetero_1, homo_rec_1), ncol = 3);
	mut2_matrix = matrix(c(homo_dom_2, hetero_2, homo_rec_2), ncol = 3);
	genotype_matrix = matrixMult(t(mut1_matrix), mut2_matrix);
	cat(sim.cycle + " ");
	catn(genotype_matrix / N);

/// Extend to diploid population (AABB, AABb, AAbb, AaBB, AaBb, Aabb, aaBB, aaBb, aabb)
/// How to get individuals' genotype fast? 
/// p1.genomes goes from individual 1 genome 1, ind1 gen 2, etc. just need to make has_mut1 and has_mut2 2 X N matrices.

	
	if (genotype_matrix[0,0] == N)
	{
		sim.simulationFinished();
	}
}
